//created on: 14 nov 2024
package rule1

//list any import classes here.
import conceptos.seres.*;
import conceptos.relaciones.*;
import conceptos.objetos.*;
import conceptos.capacidades.*;
import java.util.*;

//declare any global variables here

/*
ESTRUCTURA REGLA

rule ""
	when
	
	then
	
end

*/

rule "Si lo mata -> se queda con sus objetos"
	when
		$asesino: Ser()
		Mata(sujeto == $asesino, $asesinado: receptor)
		$posesionAsesinado: Posee(sujeto == $asesinado, $obj: objeto)
	then
		insert(new Posee($asesino, $obj));
		delete($posesionAsesinado);
		System.out.println("Al haber matado a " +
			$asesinado.getNombre() + ", " +
			$asesino.getNombre() + " consigue " + $obj.getNombre());
end


rule "Enojado Poseidon con Casio -> Libera a Ceto"
    //include attributes such as "salience" here...
    when
        $casiopea: Mortal(nombre == "Casiopea", !muerto)
        $poseidon: DiosMayor(nombre == "Poseidon", !muerto) and EnojadoCon(sujeto == $poseidon, receptor == $casiopea)
        $ceto: Criatura(nombre == "Ceto", !muerto) and $presoCeto: Apresa(sujeto == $poseidon, receptor == $ceto)
    then
        delete($presoCeto);
        insert(new Libera($poseidon, $ceto));
		System.out.println("Ceto ha sido liberado por Poseidón");
end

rule "Ceto liberado -> Andromeda apresada."
	when
		$ceto: CriaturaMitologica(nombre == "Ceto", !muerto) and Libera(receptor == $ceto)
		$andromeda: Ser(nombre == "Andromeda", !muerto) and not(Apresa(receptor == $andromeda))
		not(Libera(receptor == $andromeda))
	then
		insert(new Apresa($ceto, $andromeda));
		System.out.println("Andrómeda ha sido apresada por Ceto");
end

rule "Ceto liberado -> Andromeda apresada y deja de estar liberada."
	when
		$ceto: CriaturaMitologica(nombre == "Ceto", !muerto) and Libera(receptor == $ceto)
		$andromeda: Ser(nombre == "Andromeda", !muerto) and not(Apresa(receptor == $andromeda))
		$liberadaAndro: Libera(receptor == $andromeda)
	then
		delete($liberadaAndro);
		insert(new Apresa($ceto, $andromeda));
		System.out.println("Andrómeda ha sido apresada por Ceto y deja de estar liberada");
end


rule "Favor de Hades -> Casco de Hades"
	when
		$casco: Objeto(nombre == "Casco de Hades")
		$hades: DiosMayor(nombre == "Hades", !muerto) and $poseeCascoHades: Posee(sujeto == $hades, objeto == $casco)
		$h: Heroe() and Favorece(sujeto == $hades, receptor == $h)		
	then
		insert(new Posee($h, $casco));
		delete($poseeCascoHades);
		System.out.println("Gracias al favor de Hades, " + $h.getNombre() + " ha" +
			" conseguido el Casco de Hades");			
end

rule "Favor de Atenea -> Escudo de Atenea"
	when
		$escudo: Objeto(nombre == "Escudo de Bronce")
		$atenea: DiosMayor(nombre == "Atenea", !muerto)
		$poseeAteneaEscudo: Posee(sujeto == $atenea, objeto == $escudo)
		$h: Heroe(!muerto) and Favorece(sujeto == $atenea, receptor == $h)
	then
		insert(new Posee($h, $escudo));
		delete($poseeAteneaEscudo);
		System.out.println("Gracias al favor de Atenea, " + $h.getNombre() + " ha" +
			" conseguido el Escudo de Bronce");
end

rule "Favor Hefesto -> Espada"
	when
		$espada: ObjetoMitologico(nombre == "Espada Indestructible")
		$hefesto: DiosMayor(nombre == "Hefesto", !muerto)
		$poseeHefestoEsp: Posee(sujeto == $hefesto, objeto == $espada)
		$h: Heroe(!muerto) and Favorece(sujeto == $hefesto, receptor == $h)
	then
		insert(new Posee($h, $espada));
		delete($poseeHefestoEsp);
		System.out.println("Gracias al favor de Hefesto, " + $h.getNombre() + " ha" +
			" conseguido la Espada Indestructible");
end

rule "Favor Hermes -> Hoz"
	when
		$hoz: Objeto(nombre == "Hoz de Acero")
		$hermes: DiosMayor(nombre == "Hermes", !muerto)
		$poseeHermesHoz: Posee(sujeto == $hermes, objeto == $hoz)
		$h: Heroe(!muerto) and Favorece(sujeto == $hermes, receptor == $h)
	then
		insert(new Posee($h, $hoz));
		delete($poseeHermesHoz);
		System.out.println("Gracias al favor de Hermes, " + $h.getNombre() + " ha" +
			" conseguido la hoz de acero");
end

rule "Favores de Hermes, Atenea y Hades -> localiza Grayas"
	when
		$hermes: DiosMayor(nombre == "Hermes", !muerto)
		$atenea: DiosMayor(nombre == "Atenea", !muerto)
		$hades: DiosMayor(nombre == "Hades", !muerto)
		$grayas: DiosMenor(nombre == "Grayas")
		$h: Heroe(!muerto) and not(Localiza(sujeto == $h, receptor == $grayas))
		Favorece(sujeto == $hermes, receptor == $h)
		Favorece(sujeto == $atenea, receptor == $h)
		Favorece(sujeto == $hades, receptor == $h)
	then
		insert(new Localiza($h, $grayas));
		System.out.println($h.getNombre() + " ha localizado a las Grayas");
end

rule "Localiza Grayas -> roba ojo"
	when
		$ojo: Objeto(nombre == "Ojo Grayas")
		$grayas: DiosMenor(nombre == "Grayas", !muerto)
		$poseenOjoGrayas:= Posee(sujeto == $grayas, objeto == $ojo)
		$h: Heroe(!muerto) and Localiza(sujeto == $h, receptor == $grayas)
		(Posee(sujeto == $h, $ob: objeto) and Invisibilidad(objeto == $ob)) or Invisibilidad(sujeto == $h)
		$ninfas:= DiosMenor(nombre == "Ninfas")
		not(Localiza(sujeto == $h, receptor == $ninfas))
	then
		insert(new Posee($h, $ojo));
		delete($poseenOjoGrayas);
		System.out.println($h.getNombre() + " ha robado a las Grayas su ojo");
end

rule "Devuelve ojo -> localiza Ninfas"
	when
		$ojo: Objeto(nombre == "Ojo Grayas")
		$grayas: DiosMenor(nombre == "Grayas", !muerto)
		$ninfas: DiosMenor(nombre == "Ninfas")
		$h: Heroe(!muerto)
		not(Localiza(sujeto == $h, receptor == $ninfas) or Posee(sujeto == $grayas, objeto == $ojo))
		$poseeHeroeOjo:= Posee(sujeto == $h, objeto == $ojo)
	then
		insert(new Posee($grayas, $ojo));
		delete($poseeHeroeOjo);
		insert(new Localiza($h, $ninfas));
		System.out.println("Las Grayas recuperan su ojo");
		System.out.println($h.getNombre() + " localiza a las Ninfas");
end

rule "Localizadas ninfas -> consigue objetos"
	when
		$ninfas: DiosMenor(nombre == "Ninfas", !muerto)
		$h: Heroe(!muerto) and Localiza(sujeto == $h, receptor == $ninfas)
		$posesionNinfas: Posee(sujeto == $ninfas, $obj: objeto)
	then
		insert(new Posee($h, $obj));
		delete($posesionNinfas);
		System.out.println("Por localizar a las Ninfas, " + $h.getNombre() + " posee " + $obj.getNombre());
end

rule "Tiene sandalias, vuelo, algo cortante y algo reflectante -> mata a Medusa"
	when
		$h: Heroe(!muerto)
		Posee(sujeto == $h, $ob1: objeto) and Vuelo(objeto == $ob1) //or Vuelo(sujeto == $h)
		Posee(sujeto == $h, $ob2: objeto) and Letal(objeto == $ob2) //or Letal(sujeto == $h)
		Posee(sujeto == $h, $ob3: objeto) and Reflectante(objeto == $ob3)// or Reflectante(sujeto == $h)
		$medusa: CriaturaMitologica(nombre == "Medusa", !muerto)
	then
		// MUERTE MEDUSA
		modify($medusa){setMuerto(true);}
		insert(new Mata($h, $medusa));
		// Se inserta la cabeza de Medusa como objeto.
		ObjetoMitologico cabezaMedusa = new ObjetoMitologico("Cabeza de Medusa");
		insert(cabezaMedusa);
		insert(new Petrificar(cabezaMedusa));		
		System.out.println($h.getNombre() + " ha matado a Medusa");
end

rule "Si alguien ha matado a Medusa -> consigue su cabeza"
	when
		$s: Ser(!muerto)
		$medusa: CriaturaMitologica(nombre == "Medusa")
		Mata(sujeto == $s, receptor == $medusa)
		$zurron: ObjetoMitologico(nombre == "Zurron Magico")
		Posee(sujeto == $s, objeto == $zurron)
		$cabezaMedusa: ObjetoMitologico(nombre == "Cabeza de Medusa");
	then
		insert(new Posee($s, $cabezaMedusa));
		System.out.println($s.getNombre() + " consigue la cabeza de Medusa");
end

rule "Tiene algo petrificante -> mata a Ceto"
	when
		$heroe: Heroe()
		(Posee(sujeto == $heroe, $petrificante: objeto) and Petrificar(objeto == $petrificante)) or Petrificar(sujeto == $heroe)
		$ceto: CriaturaMitologica(nombre == "Ceto")
	then
		insert(new Mata($heroe, $ceto));
		modify($ceto){setMuerto(true);}
		System.out.println($heroe.getNombre() + " ha matado a Ceto");
end

rule "Si matas a alguien -> liberas a sus prisioneros"
	when
		$s: Ser()
		Mata(sujeto == $s, $asesinado: receptor)
		$prision: Apresa(sujeto == $asesinado, $apresado: receptor)
	then
		insert(new Libera($s, $apresado));
		delete($prision);
		System.out.println($s.getNombre() + " ha liberado a " + $apresado.getNombre());
end

// REGLA DE PRUEBA

rule "Cuando Andromeda se libera, se descubre que Zeus es Padre de Kratos"
	when
		$andromeda: Ser(nombre == "Andromeda", !muerto)
		$zeus: DiosMayor(nombre == "Zeus")
		$kratos: Semidios(!muerto)
		Libera(receptor == $andromeda)
		not(Padre(sujeto == $zeus, receptor == $kratos))
	then
		insert(new Padre($zeus, $kratos));
		System.out.println("Se descubre que " + $kratos.getNombre() + " es hijo de Zeus");
end

rule "Si Zeus es el padre de Kratos -> le entrega las espadas del caos"
	when
		$espadas: ObjetoMitologico(nombre == "Espadas del Caos") or $espadas: ObjetoMitologico(nombre == "Telas de Java Moradas" )
		$zeus: DiosMayor(nombre == "Zeus") and $poseeZeusEspadas: Posee(sujeto == $zeus, objeto == $espadas)
		$kratos: Semidios()
		Padre(sujeto == $zeus, receptor == $kratos)
	then
		insert(new Posee($kratos, $espadas));
		delete($poseeZeusEspadas);
		System.out.println("Zeus le otorga las " + $espadas.getNombre() + " a " + $kratos.getNombre());
end

rule "Si tienes las espadas del Caos -> puedes matar cualquier cosa localizada"
	when
		$s: Ser(!muerto, nombre != "Zeus")
		$espadasDelCaos: ObjetoMitologico(nombre == "Espadas del Caos") or $espadasDelCaos: ObjetoMitologico(nombre == "Telas de Java Moradas" )
		Posee(sujeto == $s, objeto == $espadasDelCaos)
		$cualquiera: Ser(!muerto)
		Localiza(sujeto == $s, receptor == $cualquiera)
	then
		insert(new Mata($s, $cualquiera));
		modify($cualquiera){setMuerto(true);}
		System.out.println($cualquiera.getNombre() + " ha muerto a manos de " + $s.getNombre());
end

/*
rule "Ceto muerto y tiene alguien apresado -> el asesino de ceto lo libera"
	when
		$ser: Ser()
		$preso: Apresado(sujeto == $ser)
		$ceto: CriaturaMitologica(nombre == "Ceto", ha_apresado_a contains $ser, $asesino := matado_por)
		Muerto(sujeto == $ceto)
	then
		modify($asesino){addLiberado($ser);}
		modify($ser){setApresado_por(null), setLiberado_por($asesino);}
		modify($ceto){eliminarPrisionero($ser);}
		insert(new Libre($ser));
		delete($preso);
		System.out.println($ser.getNombre() + " ha sido liberado por " + $asesino.getNombre());		
end
*/