//created on: 15 nov 2024
package rule2

//list any import classes here.
import conceptos.seres.*;
import conceptos.relaciones.*;
import conceptos.objetos.*;
import conceptos.capacidades.*;
import java.util.*;
import conceptos.seres.TipoCriatura;


//declare any global variables here


// // **************************REGLAS GENERALES************************** //

rule "Informar inicio"
	salience 10
	agenda-group "informacion"
	when
		$o: Object(!($o instanceof Ser), !($o instanceof Objeto))
	then
		System.out.println($o.toString());
end

rule "Si alguien que tenia el favor de un Dios ahora tiene su enojo -> ya no tiene el favor"
	salience 10
	agenda-group "generales"
	when
		$enojo: EnojadoCon($dios:= sujeto, $mortal:= receptor)
		$favor: Favorece(sujeto == $dios, receptor == $mortal)
	then
		delete($favor);
		System.out.println($enojo.toString() +", por lo que no " + $favor.toString());
end

rule "Si estabas libre y ahora estas apresado -> ya no estas libre"
	salience 10
	agenda-group "generales"
	when
		$libre: Libera($ser:= receptor)
		Apresa(receptor == $ser)
	then
		delete($libre);
end

rule "Si tu padre es Dios Mayor -> tienes su favor"
	salience 10
	agenda-group "generales"
	when
		$dios: DiosMayor()
		$mortal: Mortal()
		$relacionPadre: Padre(sujeto == $dios, receptor == $mortal)
	then
		Favorece favor = new Favorece($dios, $mortal);
		insert(favor);
		System.out.println($relacionPadre.toString() + ", por lo que " + favor.toString());
end

rule "Si tienes localizado a un muerto (que nadie haya matado) con objetos -> te puedes quedar sus objetos"
	salience 10
	agenda-group "generales"
	when
		$ser: Ser(!muerto)
		$muerto: Ser(muerto)
		not(Mata(receptor == $muerto))
		$posesionMuerto: Posee(sujeto == $muerto, $obj:= objeto)
		$localizado: Localiza(sujeto == $ser, receptor == $muerto)
	then
		Posee posee = new Posee($ser, $obj);
		insert(posee);
		delete($posesionMuerto);
		System.out.println($localizado.toString() + " y " + $muerto.toString() + " esta muerto , por lo que " +
			 posee.toString());
end

rule "x mata a y -> x tiene el inventario de y"
	salience 10
	agenda-group "generales"
	when
		$mata: Mata($asesino:= sujeto, $muerto:= receptor)
		$posesionMuerto: Posee(sujeto == $muerto, $obj:= objeto)
	then
		insert(new Posee($asesino, $obj));
		delete($posesionMuerto);
		System.out.println($mata.toString() + ", por lo que " +
			$asesino.toString() + " consigue " + $obj.toString());
end

rule "x tiene el Favor de un Dios -> x tiene su inventario"
	salience 10
	agenda-group "generales"
    //include attributes such as "salience" here...
    when
        $favor: Favorece($dios:= sujeto, $mortal:= receptor)
        $pos: Posee(sujeto == $dios, $obj:= objeto)
    then
        insert(new Posee($mortal, $obj));
        delete($pos);
        System.out.println($favor.toString() +	", por lo que " + 
        	$mortal.toString() + " consigue " + $obj.toString());
end

// Voy a suponer que, cuando tres dioses te ofrecen un favor, puedes localizar aquellos que los 3 localizan.
rule "x tiene favor de tres dioses que localizan a y -> x localiza a y"
	salience 10
	agenda-group "generales"
	when
		$mortal: Mortal()
		Favorece($dios1:= sujeto, receptor == $mortal) and Localiza(sujeto == $dios1, $localizado:= receptor)
		$dioses: List(size >= 2) from
			accumulate(Favorece($dios:= sujeto, $dios != $dios1, receptor == $mortal) and Localiza(sujeto == $dios, receptor == $localizado); collectList($dios))		
		not(Localiza(sujeto == $mortal, receptor == $localizado))
	then
		insert(new Localiza($mortal, $localizado));
		System.out.println("Por tener el favor de " +
			$dios1.getNombre() + ", " + ((Dios)$dioses.get(0)).getNombre() + "  y " + ((Dios)$dioses.get(1)).getNombre() + ", " + $mortal.getNombre() + " ha localizado a " +
			$localizado.getNombre());
end

rule "Si matas a alguien -> liberas a sus prisioneros"
	salience 10
	agenda-group "generales"
	when
		$s: Ser()
		$mata: Mata(sujeto == $s, $asesinado: receptor)
		$prision: Apresa(sujeto == $asesinado, $apresado: receptor)
	then
		Libera libera = new Libera($s, $apresado);
		insert(libera);
		delete($prision);
		System.out.println($mata.toString() + ", por lo que " + libera.toString());
end

// *************************************************************** //

rule "Poseidon enojado con Casiopea -> Poseidon libera a Ceto"
	salience 5
	agenda-group "generales"
    when
        $pos: DiosMayor(nombre == "Poseidon", !muerto)
        $casiopea: Mortal(nombre == "Casiopea", !muerto)
        $enojadoPos: EnojadoCon(sujeto == $pos, receptor == $casiopea)
        $criaturaMarina: CriaturaMitologica(tipo == TipoCriatura.MARINA, !muerto)
        $apres: Apresa(sujeto == $pos, receptor == $criaturaMarina)
       	$otraCriatura: CriaturaMitologica(!muerto) and not(Libera(sujeto == $pos, receptor == $otraCriatura))
    then
        delete($apres);
        Libera libera = new Libera($pos, $criaturaMarina);
        insert(libera);
		System.out.println($enojadoPos.toString() + ", por lo que " + libera.toString()); 
end

rule "PoseidÃ³n ha liberado una criatura marina y enojado con Casiopea -> la criatura marina apresa a su hijo"
	salience 5
	agenda-group "generales"
	when
		$pos: DiosMayor(nombre == "Poseidon", !muerto)
		$criatura: CriaturaMitologica(tipo == TipoCriatura.MARINA, !muerto)
		Libera(sujeto == $pos, receptor == $criatura)
        $casiopea: Mortal(nombre == "Casiopea", !muerto)
        $enojo: EnojadoCon(sujeto == $pos, receptor == $casiopea)
        $hijo: Ser(!muerto) and Padre(sujeto == $casiopea, receptor == $hijo)
        not(Apresa(receptor == $hijo))
	then
		Apresa apresa = new Apresa($criatura, $hijo);
		insert(apresa);
		System.out.println($enojo.toString() + ", por lo que " + apresa.toString());
end

rule "x Localiza a las Grayas y Grayas localiza a alguien que no localiza x -> x roba ojo"
	salience 5
	agenda-group "generales"
	when
		$h: Heroe(!muerto)
		(Posee(sujeto == $h, $obj:= objeto) and Invisibilidad(objeto == $obj)) or Invisibilidad(sujeto == $h)
		$grayas: DiosMenor(nombre == "Grayas")
		$localiza: Localiza(sujeto == $h, receptor == $grayas)
		$ojo: Objeto(nombre == "OjoGrayas")
		$poseenOjoGrayas: Posee(sujeto == $grayas, objeto == $ojo)
		Localiza(sujeto == $grayas, $alguien:= receptor) and not(Localiza(sujeto == $h, receptor == $alguien))
	then
		insert(new Posee($h, $ojo));
		delete($poseenOjoGrayas);
		System.out.println($localiza.toString() + ", por lo que " +
			$h.toString() + " ha robado a " + $grayas.toString() + " " + $ojo.toString());
end

rule "x devuelve ojo a las Grayas -> x tiene localizados los que localizan las Grayas"
	salience 5
	agenda-group "generales"
	when
		$ojo: Objeto(nombre == "OjoGrayas")
		$grayas: DiosMenor(nombre == "Grayas", !muerto)
		$h: Heroe(!muerto)
		Localiza(sujeto == $grayas, $localizado:= receptor)
		$poseeHeroeOjo:= Posee(sujeto == $h, objeto == $ojo)
	then
		insert(new Posee($grayas, $ojo));
		delete($poseeHeroeOjo);
		Localiza localiza = new Localiza($h, $localizado);
		insert(localiza);
		System.out.println($h.toString() + " devuelve a " + $grayas.toString() + " " + $ojo.toString());
		System.out.println(localiza.toString());
end

rule "x tiene objetos letal, vuelo, y reflectante -> mata a Medusa"
	salience 5
	agenda-group "generales"
	when
		$h: Heroe(!muerto)
		$pos1: Posee(sujeto == $h, $ob1: objeto) and $vuelo: Vuelo(objeto == $ob1) 
		$pos2: Posee(sujeto == $h, $ob2: objeto) and $letal: Letal(objeto == $ob2) //or Corte(sujeto == $h)
		$pos3: Posee(sujeto == $h, $ob3: objeto) and $reflectante: Reflectante(objeto == $ob3)// or Reflectante(sujeto == $h)
		$medusa: CriaturaMitologica(nombre == "Medusa", !muerto)
	then
		modify($medusa){setMuerto(true);}
		Mata mata = new Mata($h, $medusa); 
		insert(mata);
		System.out.println($pos1.toString() + ", " + $pos2.toString() + " y " + $pos3.toString() + ", con " +
			$vuelo.toString() + ", " + $letal.toString() + " y " + $reflectante.toString() + ", por lo que " +
			mata.toString());
end

rule "Localizadas ninfas -> consigue objetos"
	salience 5
	agenda-group "generales"
	when
		$ninfas: DiosMenor(nombre == "Ninfas", !muerto)
		$h: Heroe(!muerto) and $localiza: Localiza(sujeto == $h, receptor == $ninfas)
		$posesionNinfas: Posee(sujeto == $ninfas, $obj: objeto)
	then
		Posee posee = new Posee($h, $obj);
		insert(posee);
		delete($posesionNinfas);
		System.out.println($localiza.toString() + ", por lo que " + posee.toString());
end

rule "Si Medusa ha muerto -> aparece su cabeza"
	salience 5
	agenda-group "generales"
	when
		$medusa: CriaturaMitologica(nombre == "Medusa", muerto)
	then
		ObjetoMitologico cabezaMedusa = new ObjetoMitologico("CabezaMedusa");
		insert(cabezaMedusa);
		insert(new Petrificar(cabezaMedusa));
		//System.out.println(cabezaMedusa.toString());
end

rule "Tiene algo petrificante -> mata a Ceto"
	salience 5
	agenda-group "generales"
	when
		$heroe: Heroe(!muerto)
		(Posee(sujeto == $heroe, $petrificante: objeto) and Petrificar(objeto == $petrificante)) or Petrificar(sujeto == $heroe)
		$ceto: CriaturaMitologica(nombre == "Ceto", !muerto)
	then
		Mata mata = new Mata($heroe, $ceto);
		insert(mata);
		modify($ceto){setMuerto(true);}
		System.out.println(mata.toString());
end

rule "Si alguien ha matado a Medusa y no tiene contenedor -> muere"
	salience 5
	agenda-group "generales"
	when
		$medusa: CriaturaMitologica(nombre == "Medusa", muerto)
		$cabezaMedusa: ObjetoMitologico(nombre == "CabezaMedusa")
		Mata($ser:= sujeto, receptor == $medusa)
		not(Contenedor($obj:= objeto) and Posee(sujeto == $ser, objeto == $obj))
	then
		modify($ser){setMuerto(true);}
		System.out.println($ser.toString() + " no posee Contenedor, por lo que muere");
		//System.out.println($s.getNombre() + " consigue la cabeza de Medusa");
end

rule "Si alguien ha matado a Medusa y tiene contenedor -> consigue su cabeza"
	salience 5
	agenda-group "generales"
	when
		$medusa: CriaturaMitologica(nombre == "Medusa", muerto)
		$cabezaMedusa: ObjetoMitologico(nombre == "CabezaMedusa")
		Mata($ser:= sujeto, receptor == $medusa)
		Contenedor($obj:= objeto) and Posee(sujeto == $ser, objeto == $obj)
	then
		Posee posee = new Posee($ser, $cabezaMedusa);
		insert(posee);
		System.out.println(posee.toString());
		//System.out.println($s.getNombre() + " consigue la cabeza de Medusa");
end

// Reglas Minotauro
rule "Minos tiene favor de Poseidon -> Doma al Toro de Creta"
	salience 5
	agenda-group "generales"
	when
		$minos: Semidios(nombre == "Minos", !muerto)
		$poseidon: DiosMayor(nombre == "Poseidon")
		$toroCreta: CriaturaNormal(nombre == "ToroCreta")
		$favorece: Favorece(sujeto == $poseidon, receptor == $minos)
	then
		Doma doma = new Doma($minos, $toroCreta);
		insert(doma);
		System.out.println($favorece.toString() + ", por lo que " + doma.toString());
end

rule "Si Minos tiene domado el Toro de Creta -> Poseidon se enfada con Minos"
	salience 5
	agenda-group "generales"
	when
		$minos: Semidios(nombre == "Minos", !muerto)
		$poseidon: DiosMayor(nombre == "Poseidon")
		$toroCreta: CriaturaNormal(nombre == "ToroCreta")
		$domaMinosToro: Doma(sujeto == $minos, receptor == $toroCreta)
	then
		EnojadoCon enojoPoseMinos = new EnojadoCon($poseidon, $minos);
		insert(enojoPoseMinos);
		System.out.println($domaMinosToro.toString() + ", por lo que " + enojoPoseMinos.toString());
end

rule "Si Poseidon se enfada con Minos y Minos doma al Toro de Creta -> Pasifae se enamora del Toro"
	salience 5
	agenda-group "generales"
	when
		$poseidon: DiosMayor(nombre == "Poseidon")
		$minos: Semidios(nombre == "Minos")
		$enojoPosMinos: EnojadoCon(sujeto == $poseidon, receptor == $minos)
		$toroDeCreta: CriaturaNormal(nombre == "ToroCreta")
		$domado: Doma(sujeto == $minos, receptor == $toroDeCreta)
		$pasifae: Semidios(nombre == "Pasifae", !muerto)
	then
		Enamora amorPasifaeToro = new Enamora($pasifae, $toroDeCreta);
		insert(amorPasifaeToro);
		System.out.println($enojoPosMinos.toString() + " y " + $domado.toString() + ", por lo que " +
			amorPasifaeToro.toString());
end

rule "Si Pasifae se enamora del Toro de Creta -> Aparece el Minotauro con un objeto Cuerno"
	salience 5
	agenda-group "generales"
	when
		$pasifae: Semidios(nombre == "Pasifae", !muerto)
		$toroCreta: CriaturaNormal(nombre == "ToroCreta", !muerto)
		$enamorada: Enamora(sujeto == $pasifae, receptor == $toroCreta)
	then
		CriaturaMitologica minotauro = new CriaturaMitologica("Minotauro", TipoCriatura.TERRESTRE);
		ObjetoMitologico cuernoMinotauro = new ObjetoMitologico("CuernoMinotauro");
		Posee posMinotCuerno = new Posee(minotauro, cuernoMinotauro);
		insert(minotauro);
		insert(cuernoMinotauro);
		insert(posMinotCuerno);
		System.out.println($enamorada.toString() + ", por lo que nace el " + minotauro.toString());
end

rule "Si Minotauro esta libre e Icaro y Dedalo estan vivos -> Minos Apresa al Minotauro"
	salience 5
	agenda-group "generales"
	when
		$minotauro: CriaturaMitologica(nombre == "Minotauro", !muerto) and not(Apresa(receptor == $minotauro))
		$minos: Semidios(nombre == "Minos", !muerto)
		$icaro: Humano(nombre == "Icaro", !muerto) and not(Apresa(receptor == $icaro))
		$dedalo: Humano(nombre == "Dedalo", !muerto) and not(Apresa(receptor == $dedalo))
	then
		Apresa minosApresaMinot = new Apresa($minos, $minotauro);
		insert(minosApresaMinot);
		System.out.println($dedalo.toString() + " construye el Laberinto de Creta");
		System.out.println(minosApresaMinot.toString() + " en el Laberinto de Creta");
end

rule "Si Ariadna esta enamorada de un heroe -> le da sus objetos"
	salience 10
	agenda-group "generales"
	when
		$ariadna: Mortal(nombre == "Ariadna", !muerto)
		$heroe: Heroe(!muerto)
		$enamorada: Enamora(sujeto == $ariadna, receptor == $heroe)
		$poseeAriadna: Posee(sujeto == $ariadna, $objeto:= objeto)
	then
		delete($poseeAriadna);
		Posee poseeHeroe = new Posee($heroe, $objeto);
		insert(poseeHeroe);
		System.out.println($enamorada.toString() + ", por lo que le entrega " + $objeto.toString());
end

rule "Si un heroe tiene invisibilidad y conoce la localizacion de Ariadna -> Ariadna se enamora del heroe"
	salience 5
	agenda-group "generales"
	when
		$heroe: Heroe(!muerto)
		(Invisibilidad(sujeto == $heroe) or 
			(Posee(sujeto == $heroe, $obj:= objeto) and Invisibilidad(objeto == $obj)))
		$ariadna: Mortal(nombre == "Ariadna", !muerto)
		$localiza: Localiza(sujeto == $heroe, receptor == $ariadna)
		not(Enamora(sujeto == $ariadna, receptor == $heroe))	// No se va a enamorar dos veces
	then
		Enamora enamAriadnaHeroe = new Enamora($ariadna, $heroe);
		insert(enamAriadnaHeroe);
		System.out.println($localiza.toString() + " y se vuelve invisible y se entera de los gustos"
			+ " de " + $ariadna.toString() + ", por lo que " + enamAriadnaHeroe.toString());
end

rule "Si Ariadna se enamora de alguien -> Minos se enfada con el"
	salience 10
	agenda-group "generales"
	when
		$ariadna: Mortal(nombre == "Ariadna")
		$enamorada: Enamora(sujeto == $ariadna, $amor:= receptor)
		$minos: Semidios(nombre == "Minos")
	then
		EnojadoCon enojo = new EnojadoCon($minos, $amor);
		insert(enojo);
		System.out.println($enamorada.toString() + ", por lo que " + enojo.toString());
end

rule "Si Minos se enfada con Mortal -> lo encierra en el laberinto y localiza Minotauro"
	salience 5
	agenda-group "generales"
	when
		$minos: Semidios(nombre == "Minos", !muerto)
		$minotauro: CriaturaMitologica(nombre == "Minotauro") 
		Apresa(sujeto == $minos, receptor == $minotauro)
		EnojadoCon(sujeto == $minos, $mortal:= receptor)
		not(Apresa(receptor == $mortal))	// Comprobar que no estaba apresado previamente.
	then
		Apresa apresaMinosMortal = new Apresa($minos, $mortal);
		Localiza localizaMortalMinot = new Localiza($mortal, $minotauro);
		insert(apresaMinosMortal);
		insert(localizaMortalMinot);
		System.out.println($minos.toString() + " ha encerrado a " + $mortal.toString() +
			" en el laberinto");	
end

rule "Si estas Apresado por Minos y el Minotauro esta muerto y hay mas encerrados por Minos -> liberas al Apresado"
	salience 5
	agenda-group "generales"
	when
		$minos: Semidios(nombre == "Minos")
		$minotauro: CriaturaMitologica(nombre == "Minotauro", muerto)
		Apresa(sujeto == $minos, $ser:= receptor)
		(Imperdible(sujeto == $ser) or 
			(Posee(sujeto == $ser, $obj:= objeto) and Imperdible(objeto == $obj)))
		$apresado: Apresa(sujeto == $minos, $otro:= receptor) 
	then
		Libera liberaSerOtro = new Libera($ser, $otro);
		insert(liberaSerOtro);
		delete($apresado);
		System.out.println($ser.toString() + " ha conseguido que " + $otro.toString() +
			" salga del laberinto");
end

rule "Si estas Apresado por Minos y el Minotauro esta vivo y no puedes matarlo -> Te apresa"
	salience 5
	agenda-group "generales"
	when
		$minos: Semidios(nombre == "Minos")
		$minotauro: CriaturaMitologica(nombre == "Minotauro", !muerto)
		$apresado: Apresa(sujeto == $minos, $ser:= receptor, $ser != $minotauro)
		not(Letal(sujeto == $ser) or 
			(Posee(sujeto == $ser, $obj:= objeto) and Letal(objeto == $obj))) 
	then
		delete($apresado);
		Apresa apresaMinotauro = new Apresa($minotauro, $ser);
		insert(apresaMinotauro);
		System.out.println($ser.toString() + " esta en el laberinto y no puede matar a " + $minotauro.toString() +
			", por lo que " + apresaMinotauro.toString()); 
end

rule "Si estas Apresado por Minos y el Minotauro esta vivo y puedes matarlo -> lo matas"
	salience 5
	agenda-group "generales"
	when
		$minos: Semidios(nombre == "Minos")
		$minotauro: CriaturaMitologica(nombre == "Minotauro", !muerto)
		$apresado: Apresa(sujeto == $minos, $ser:= receptor, $ser != $minotauro)
		Localiza(sujeto == $ser, receptor == $minotauro)
		(Letal(sujeto == $ser) or 
			(Posee(sujeto == $ser, $obj:= objeto) and Letal(objeto == $obj))) 
	then
		Mata mataMinotauro = new Mata($ser, $minotauro);
		insert(mataMinotauro);
		modify($minotauro){setMuerto(true);}
		System.out.println($ser.toString() + " esta en el laberinto y " + mataMinotauro.toString()); 
end

rule "Si estas Apresado por Minos y el Minotauro esta muerto -> te marchas"
	salience 5
	agenda-group "generales"
	when
		$minos: Semidios(nombre == "Minos")
		$minotauro: CriaturaMitologica(nombre == "Minotauro", muerto)
		Localiza($ser:= sujeto, receptor == $minotauro)
		$apresado: Apresa(sujeto == $minos, $ser:= receptor)
		(Imperdible(sujeto == $ser) or 
			(Posee(sujeto == $ser, $obj:= objeto) and Imperdible(objeto == $obj))) 
	then
		delete($apresado);
		System.out.println($ser.toString() + " ha conseguido salir del laberinto");
end

rule "Si estas Apresado por Minos y el Minotauro esta muerto y Icaro y Dedalo estan vivos -> te marchas y Minos se enfada"
	salience 5
	agenda-group "generales"
	when
		$minos: Semidios(nombre == "Minos")
		$minotauro: CriaturaMitologica(nombre == "Minotauro", muerto)
		$apresado: Apresa(sujeto == $minos, $ser:= receptor)
		(Imperdible(sujeto == $ser) or 
			(Posee(sujeto == $ser, $obj:= objeto) and Imperdible(objeto == $obj)))
		$icaro: Humano(nombre == "Icaro", !muerto)
		$dedalo: Humano(nombre == "Dedalo", !muerto)
	then
		EnojadoCon enojoIcaro = new EnojadoCon($minos, $icaro);
		EnojadoCon enojoDedalo = new EnojadoCon($minos, $dedalo);
		delete($apresado);
		System.out.println($ser.toString() + " ha conseguido salir del laberinto");
		System.out.println("Alguien ha salido del laberinto con vida, por lo que " +
			enojoIcaro.toString() + " y " + enojoDedalo.toString());
end

rule "Si matas al minotauro -> ahora sus prisioneros y tu estan apresados por Minos"
	salience 10
	agenda-group "generales"
	when
		$minotauro: CriaturaMitologica(nombre == "Minotauro", muerto)
		$mata: Mata(receptor == $minotauro)
		$apresadoMinot: Apresa(sujeto == $minotauro, $preso:= receptor)
		$minos: Semidios(nombre == "Minos") 
	then
		delete($apresadoMinot);
		Apresa apresa = new Apresa($minos, $preso);
		insert(apresa);
		System.out.println($mata.toString() + ", por lo que " + apresa.toString());
end

/*
rule "Si apresado por Minos, Minotauro muerto y tienes hilo -> dejas de estar apresado"
	when
		$s: Ser()
		$minotauro: CriaturaMitologica(nombre == "Minotauro", muerto)
		$hilo: ObjetoMitologico(nombre == "HiloImperdible")
		$posee: Posee(sujeto == $s, objeto == $hilo) or
			(Libera(sujeto == $s, $liberado:= receptor) and Posee(sujeto == $liberado, objeto == $hilo)) 
		$minos: Ser(nombre == "Minos")
		$apresado: Apresa(sujeto == $minos, receptor == $s)
	then
		delete($apresado);
		System.out.println("Ya no " + $apresado.toString());
end

rule "Si apresado por Minos, Minotauro muerto y tu liberador libre -> dejas de estar apresado"
	when
		$minos: Ser(nombre == "Minos")
		$s: Ser() and $apresado: Apresa(sujeto == $minos, receptor == $s)
		$liberadoMinotauro: Libera($liberador:= sujeto, receptor == $s)
		not(Apresa(sujeto == $minos, receptor == $liberador))
	then
		delete($apresado);
		delete($liberadoMinotauro);
		Libera liberadoMinos = new Libera($liberador, $s);
		System.out.println($liberador.toString() + "esta libre y " + $liberadoMinotauro.toString() + ", por lo que " +
			"ya no " + $apresado.toString());		
end*/